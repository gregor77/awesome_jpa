buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

jar {
    baseName = 'awesome_jpa'
    version =  '0.1.0_SNAPSHOT'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    generated {
        java {
            srcDirs = ['src/main/generated']
        }
    }
}

configurations {
    querydslapt
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("postgresql:postgresql:9.1-901.jdbc4")
    compile("org.projectlombok:lombok:1.16.8")

    def queryDSL = '3.6.3'
    compile("com.mysema.querydsl:querydsl-jpa:$queryDSL")
    querydslapt "com.mysema.querydsl:querydsl-apt:$queryDSL"

    compile("org.springframework.boot:spring-boot-starter-data-jpa")

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("junit:junit")
}

// QEntity 생성 task
task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

// compileJava task에 dependency를 걸어줍니다.
compileJava {
    dependsOn generateQueryDSL
    // compile target에 generated된 QClass들의 위치를 추가.
    generateQueryDSL.destinationDir
//    source sourceSets.generated.java.srcDirs.iterator().next()
}

compileGeneratedJava {
    dependsOn generateQueryDSL
    options.warnings = false
    classpath += sourceSets.main.runtimeClasspath
}

clean {
    delete sourceSets.generated.java.srcDirs
}

idea {
    module {
        sourceDirs += file('src/main/generated')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}